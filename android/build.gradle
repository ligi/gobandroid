buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.10.+'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.10.0'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'android'
apply plugin: 'spoon'


repositories {
    mavenCentral()
    maven {
        url project.hasProperty('ligisMavenUrl') ? ligisMavenUrl : "https://raw.github.com/ligi/ligis-mvn-repo/master";
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1"

    defaultConfig {
        versionCode 128
        versionName "1.28"
        minSdkVersion 9
        targetSdkVersion 19
        applicationId "org.ligi.gobandroid_hd"
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    flavorDimensions "analytics", "cloud"
    productFlavors {
        withAnalytics {
            flavorDimension "analytics"
        }

        noAnalytics {
            flavorDimension "analytics"
        }

        withCloud {
            flavorDimension "cloud"
        }

        noCloud {
            flavorDimension "cloud"
        }

    }

    lintOptions {
        abortOnError false
    }

    // important for androidTest
    packagingOptions {
        exclude 'LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            runProguard false
            proguardFile 'proguard-project.txt'
            applicationVariants.all { variant ->
                def file = variant.outputFile
                variant.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
            }
        }
    }


}

dependencies {
    compile 'com.android.support:support-v4:20.0.+'
    compile 'com.android.support:appcompat-v7:20.0.+'
    compile 'org.ligi:AXT:0.28'
    compile 'com.google.guava:guava:17.0'
    compile 'org.ligi:tracedroid:1.1'
    compile 'com.jakewharton:butterknife:5.1.1'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.4'

    def play_services = 'com.google.android.gms:play-services:5.0.77'

    withCloudCompile play_services
    withAnalyticsCompile play_services

    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.android.support'
        exclude group: 'com.google.guava'
    }

    androidTestCompile('com.jakewharton.espresso:espresso-support-v4:1.1-r3') {
        exclude group: 'com.android.support'
        exclude group: 'com.google.guava'
    }

    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.1'
    androidTestCompile 'com.squareup:fest-android:1.0.7'

}
