buildscript {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-alpha3'
        classpath 'de.felixschulze.gradle:gradle-spoon-plugin:2.6.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'de.mobilej.unmock:UnMockPlugin:0.3.6'

        // http://stackoverflow.com/a/33889117/322642
        classpath 'com.github.JakeWharton:sdk-manager-plugin:220bf7a88a7072df3ed16dc8466fb144f2817070'

    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'de.felixschulze.gradle.spoon'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    jcenter()
}

android {
    flavorDimensions "analytics", "cloud"

    productFlavors {
        withAnalytics {
            dimension "analytics"
        }

        noAnalytics {
            dimension "analytics"
        }

        withCloud {
            dimension "cloud"
        }

        noCloud {
            dimension "cloud"
        }

    }

    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        versionCode 217
        versionName "2.1.7"
        minSdkVersion 9
        targetSdkVersion 22
        applicationId "org.ligi.gobandroid_hd"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "Gobandroid-$versionName"
    }


    lintOptions {
        warning 'MissingTranslation'

        // for OKIO
        warning 'InvalidPackage'
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // important for androidTest
        exclude 'LICENSE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard-project.txt'
        }
    }

} // android

spoon {
    debug = true
}

final DAGGER_VERSION = '2.0.2'

dependencies {
    apt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile "com.google.dagger:dagger:${DAGGER_VERSION}"

    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'
    compile 'com.android.support:preference-v7:23.1.1'

    compile 'org.ligi:AXT:0.35'
    compile 'org.ligi:tracedroid:1.4'
    compile 'org.ligi:snackengage:0.4'

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'net.sourceforge.jchardet:jchardet:1.0'
    compile 'com.squareup.okhttp:okhttp:2.7.1'
    compile 'com.n3twork.druid:alphanum:1.0.3'

    withAnalyticsCompile 'com.google.android.gms:play-services-analytics:7.8.0'

    withCloudCompile 'com.google.android.gms:play-services-games:7.8.0'
    withCloudCompile 'com.google.android.gms:play-services-plus:7.8.0'
    withCloudCompile 'com.google.android.gms:play-services-appstate:7.8.0'
    withCloudCompile 'com.google.android.gms:play-services-gcm:7.8.0'
    withCloudCompile 'com.google.android.gms:play-services-plus:7.8.0'

    androidTestApt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'

    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'
    androidTestCompile 'com.android.support:support-annotations:23.1.1'

    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    testCompile 'com.android.support:support-annotations:23.1.1'
    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.squareup.spoon:spoon-client:1.3.0'
}
