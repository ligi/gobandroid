buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.2'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.10.0'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'

repositories {
    jcenter()
}

android {
    flavorDimensions "analytics", "cloud"

    productFlavors {
        withAnalytics {
            flavorDimension "analytics"
        }

        noAnalytics {
            flavorDimension "analytics"
        }

        withCloud {
            flavorDimension "cloud"
        }

        noCloud {
            flavorDimension "cloud"
        }

    }

    compileSdkVersion 20
    buildToolsVersion "20"

    defaultConfig {
        versionCode 191
        versionName "1.9.1"
        minSdkVersion 9
        targetSdkVersion 20
        applicationId "org.ligi.gobandroid_hd"
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }


    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    // important for androidTest
    packagingOptions {
        exclude 'LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            runProguard false
            proguardFile 'proguard-project.txt'
            applicationVariants.all { variant ->
                def file = variant.outputFile
                variant.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
            }
        }
    }

    // needed for assertJ
    packagingOptions {
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
    }


}

dependencies {
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'org.ligi:AXT:0.31'
    compile 'com.google.guava:guava:18.0'
    compile 'org.ligi:tracedroid:1.4'
    compile 'com.jakewharton:butterknife:5.1.2'

    def play_services = 'com.google.android.gms:play-services:5.0.89'

    withCloudCompile play_services
    withAnalyticsCompile play_services

    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.android.support'
        exclude group: 'com.google.guava'
    }

    androidTestCompile('com.jakewharton.espresso:espresso-support-v4:1.1-r3') {
        exclude group: 'com.android.support'
        exclude group: 'com.google.guava'
    }

    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.1'
}
