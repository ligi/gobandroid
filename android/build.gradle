apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.getkeepsafe.dexcount'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

android {
    flavorDimensions "analytics", "cloud", "build"

    productFlavors {
        withAnalytics {
            dimension "analytics"
        }

        noAnalytics {
            dimension "analytics"
        }

        withCloud {
            dimension "cloud"
        }

        noCloud {
            dimension "cloud"
        }

        dev {
            dimension "build"
            minSdkVersion 21
        }

        prod {
            dimension "build"
        }
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        versionCode 248
        versionName "2.4.8"
        minSdkVersion 9
        targetSdkVersion 22
        applicationId "org.ligi.gobandroid_hd"
        testInstrumentationRunner "org.ligi.gobandroid_hd.AppReplacingRunner"
        archivesBaseName = "Gobandroid-$versionName"
        vectorDrawables.useSupportLibrary = true
    }


    lintOptions {
        warning 'MissingTranslation'
        warning 'ResourceType'

        // for OKIO
        warning 'InvalidPackage'
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // important for androidTest
        exclude 'LICENSE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }

        debug {
            // to avoid MultiDex http://stackoverflow.com/questions/38329248/dexindexoverflowexception-when-running-androidtests
            minifyEnabled true
            useProguard false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-project.txt'
        }
    }

} // android

spoon {
    debug = true
}

dependencies {
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.jakewharton:butterknife-compiler:$butterknife_version"

    compile 'com.google.code.findbugs:jsr305:3.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile "com.google.dagger:dagger:$dagger_version"

    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:preference-v7:$support_version"

    compile 'com.github.ligi:KAXT:0.7'
    compile 'com.github.ligi:tracedroid:1.4'
    compile 'com.github.ligi:snackengage:0.9'

    compile "com.jakewharton:butterknife:$butterknife_version"
    compile 'net.sourceforge.jchardet:jchardet:1.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'com.n3twork.druid:alphanum:1.0.3'
    compile "com.android.support:support-annotations:$support_version"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
    compile 'com.chibatching:kotpref:1.5.0'

    withAnalyticsCompile "com.google.android.gms:play-services-analytics:$play_version"

    withCloudCompile "com.google.android.gms:play-services-games:$play_version"
    withCloudCompile "com.google.android.gms:play-services-plus:$play_version"
    withCloudCompile "com.google.android.gms:play-services-gcm:$play_version"
    withCloudCompile "com.google.android.gms:play-services-plus:$play_version"

    withCloudCompile 'com.google.apis:google-api-services-games:v1-rev214-1.22.0'

    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"

    androidTestCompile('com.github.ligi:trulesk:0.5') {
        // http://stackoverflow.com/questions/30578243/why-would-adding-espresso-contrib-cause-an-inflateexception
        exclude group: 'javax.inject'
    }

    androidTestCompile "com.android.support:support-annotations:$support_version"
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'

    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    testCompile "com.android.support:support-annotations:$support_version"
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.code.findbugs:jsr305:3.0.1'

    androidTestCompile 'com.squareup.spoon:spoon-client:1.7.0'
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'

    compile 'org.greenrobot:eventbus:3.0.0'
}
