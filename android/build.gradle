buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0-rc1'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.14.1'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'

repositories {
    jcenter()
}

android {
    flavorDimensions "analytics", "cloud"

    productFlavors {
        withAnalytics {
            flavorDimension "analytics"
        }

        noAnalytics {
            flavorDimension "analytics"
        }

        withCloud {
            flavorDimension "cloud"
        }

        noCloud {
            flavorDimension "cloud"
        }

    }

    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        versionCode 199
        versionName "1.9.9"
        minSdkVersion 9
        targetSdkVersion 21
        applicationId "org.ligi.gobandroid_hd"
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }


    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    // important for androidTest
    packagingOptions {
        exclude 'LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true


            proguardFile 'proguard-project.txt'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->

                    def file = output.outputFile
                    output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
                }
            }
        }
    }

    // needed for assertJ
    packagingOptions {
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
    }


}

dependencies {
    compile 'com.android.support:support-v4:21.0.2'
    compile 'com.android.support:appcompat-v7:21.0.2'
    compile 'org.ligi:AXT:0.31'
    compile 'com.google.guava:guava:18.0'
    compile 'org.ligi:tracedroid:1.4'
    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'org.ligi:floatlabel:0.2.0'
    compile 'net.sourceforge.jchardet:jchardet:1.0'
    compile 'fr.nicolaspomepuy:discreetapprate:2.0.2@aar'
    compile 'com.android.support:support-annotations:20.0.0'

    def play_services = 'com.google.android.gms:play-services:6.1.+'

    withCloudCompile play_services
    withAnalyticsCompile play_services

    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.android.support'
        exclude group: 'com.google.guava'
    }

    androidTestCompile('com.jakewharton.espresso:espresso-support-v4:1.1-r3') {
        exclude group: 'com.android.support'
        exclude group: 'com.google.guava'
    }

    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.2'
}
