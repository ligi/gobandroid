apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.getkeepsafe.dexcount'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

android {
    flavorDimensions "analytics", "cloud", "autostart"

    productFlavors {
        withAnalytics {
            dimension "analytics"
        }

        noAnalytics {
            dimension "analytics"
        }

        withCloud {
            dimension "cloud"
        }

        noCloud {
            dimension "cloud"
        }

        withAutostart {
            dimension "autostart"
        }

        noAutostart {
            dimension "autostart"
        }

    }

    android.variantFilter { variant ->
        def analytics = variant.getFlavors().get(0).name
        def cloud = variant.getFlavors().get(1).name

        variant.setIgnore((project.hasProperty("singleFlavor") && (analytics != 'withAnalytics') && (cloud != 'withCloud')) ||
                (analytics == 'withAnalytics' && cloud == 'noCloud') ||
                (analytics == 'noAnalytics' && cloud == 'withCloud'))

    }

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        versionCode 259
        versionName "2.5.9"
        minSdkVersion 9
        targetSdkVersion 25
        applicationId "org.ligi.gobandroid_hd"
        testInstrumentationRunner "org.ligi.gobandroid_hd.AppReplacingRunner"
        archivesBaseName = "Gobandroid-$versionName"
        vectorDrawables.useSupportLibrary = true
    }


    lintOptions {
        warning 'MissingTranslation'
        warning 'ResourceType'

        // for OKIO
        warning 'InvalidPackage'
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // important for androidTest
        exclude 'LICENSE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }

        debug {
            // to avoid MultiDex http://stackoverflow.com/questions/38329248/dexindexoverflowexception-when-running-androidtests
            minifyEnabled true
            useProguard false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-project.txt'
        }
    }

} // android

spoon {
    debug = true
}


configurations {

    // http://stackoverflow.com/questions/30578243/why-would-adding-espresso-contrib-cause-an-inflateexception
    androidTestCompile.exclude group: 'com.android.support', module: 'appcompat-v7'
    androidTestCompile.exclude group: 'com.android.support', module: 'design'
    androidTestCompile.exclude group: 'com.android.support', module: 'support-v4'
    androidTestCompile.exclude group: 'com.android.support', module: 'support-annotations'
    androidTestCompile.exclude group: 'com.android.support', module: 'preference-v7'
    androidTestCompile.exclude module: 'recyclerview-v7'
    androidTestCompile.exclude module: 'kotlin-stdlib'
}

configurations {

    // http://stackoverflow.com/questions/30578243/why-would-adding-espresso-contrib-cause-an-inflateexception
    androidTestCompile.exclude group: 'com.android.support', module: 'appcompat-v7'
    androidTestCompile.exclude group: 'com.android.support', module: 'design'
    androidTestCompile.exclude group: 'com.android.support', module: 'support-v4'
    androidTestCompile.exclude group: 'com.android.support', module: 'support-annotations'
    androidTestCompile.exclude group: 'com.android.support', module: 'preference-v7'
    androidTestCompile.exclude module: 'recyclerview-v7'
}

dependencies {
    compile 'com.github.hotchemi:permissionsdispatcher:2.3.2'
    kapt 'com.github.hotchemi:permissionsdispatcher-processor:2.3.2'

    compile 'com.github.salomonbrys.kodein:kodein:3.3.0'

    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:preference-v7:$support_version"

    compile 'com.github.ligi:KAXT:0.20'
    compile 'com.github.ligi:tracedroid:1.4'

    compile 'com.github.ligi.snackengage:snackengage-playrate:0.15'

    compile 'com.github.ligi:ExtraCompats:0.4'

    compile 'net.sourceforge.jchardet:jchardet:1.0'
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile 'com.n3twork.druid:alphanum:1.0.3'
    compile "com.android.support:support-annotations:$support_version"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile("com.chibatching.kotpref:kotpref:$kotpref_version")
    compile("com.chibatching.kotpref:initializer:$kotpref_version")

    withAnalyticsCompile "com.google.android.gms:play-services-analytics:$play_version"

    withCloudCompile "com.google.android.gms:play-services-games:$play_version"
    withCloudCompile "com.google.android.gms:play-services-plus:$play_version"
    withCloudCompile "com.google.android.gms:play-services-gcm:$play_version"
    withCloudCompile "com.google.android.gms:play-services-plus:$play_version"

    withCloudCompile ('com.google.apis:google-api-services-games:v1-rev214-1.22.0') {
        exclude module: 'jsr305'
    }

    androidTestCompile 'com.github.ligi:trulesk:0.19'

    androidTestCompile "com.android.support:support-annotations:$support_version"
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'

    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    testCompile "com.android.support:support-annotations:$support_version"
    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.squareup.spoon:spoon-client:1.7.1'

    compile 'com.google.android:flexbox:0.2.6'
    compile 'org.greenrobot:eventbus:3.0.0'
}
