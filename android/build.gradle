apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'de.felixschulze.gradle.spoon'
apply plugin: 'kotlin-android'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    jcenter()
}

android {
    flavorDimensions "analytics", "cloud"

    productFlavors {
        withAnalytics {
            dimension "analytics"
        }

        noAnalytics {
            dimension "analytics"
        }

        withCloud {
            dimension "cloud"
        }

        noCloud {
            dimension "cloud"
        }

    }

    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        versionCode 238
        versionName "2.3.8"
        minSdkVersion 9
        targetSdkVersion 22
        applicationId "org.ligi.gobandroid_hd"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "Gobandroid-$versionName"
        vectorDrawables.useSupportLibrary = true
    }


    lintOptions {
        warning 'MissingTranslation'

        // for OKIO
        warning 'InvalidPackage'
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // important for androidTest
        exclude 'LICENSE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

} // android

spoon {
    debug = true
}

kapt {
    generateStubs = true
}

dependencies {
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.jakewharton:butterknife-compiler:$butterknife_version"

    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile "com.google.dagger:dagger:$dagger_version"

    compile "com.android.support:support-v4:$support_version"
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:preference-v7:$support_version"

    compile 'org.ligi:AXT:0.37'
    compile 'org.ligi:tracedroid:1.4'
    compile 'org.ligi:snackengage:0.5'

    compile "com.jakewharton:butterknife:$butterknife_version"
    compile 'net.sourceforge.jchardet:jchardet:1.0'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.n3twork.druid:alphanum:1.0.3'
    compile "com.android.support:support-annotations:$support_version"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
    compile 'com.chibatching:kotpref:1.3.0'

    withAnalyticsCompile 'com.google.android.gms:play-services-analytics:8.4.0'

    withCloudCompile 'com.google.android.gms:play-services-games:8.4.0'
    withCloudCompile 'com.google.android.gms:play-services-plus:8.4.0'
    withCloudCompile 'com.google.android.gms:play-services-appstate:8.4.0'
    withCloudCompile 'com.google.android.gms:play-services-gcm:8.4.0'
    withCloudCompile 'com.google.android.gms:play-services-plus:8.4.0'

    androidTestApt "com.google.dagger:dagger-compiler:$dagger_version"

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile "com.android.support:support-annotations:$support_version"
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'

    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    testCompile "com.android.support:support-annotations:$support_version"
    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.squareup.spoon:spoon-client:1.5.3'

    compile 'org.greenrobot:eventbus:3.0.0'
}
