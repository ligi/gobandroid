buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.0-beta1'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath('com.stanfy.spoon:spoon-gradle-plugin:1.0.0')
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'

repositories {
    jcenter()
}

android {
    flavorDimensions "analytics", "cloud"

    productFlavors {
        withAnalytics {
            flavorDimension "analytics"
        }

        noAnalytics {
            flavorDimension "analytics"
        }

        withCloud {
            flavorDimension "cloud"
        }

        noCloud {
            flavorDimension "cloud"
        }

    }

    compileSdkVersion 22
    buildToolsVersion "22"

    defaultConfig {
        versionCode 205
        versionName "2.0.5"
        minSdkVersion 9
        targetSdkVersion 22
        applicationId "org.ligi.gobandroid_hd"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            proguardFile 'proguard-project.txt'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->

                    def file = output.outputFile
                    output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
                }
            }
        }
    }


    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // important for androidTest
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:recyclerview-v7:22.0.0'
    compile 'com.android.support:cardview-v7:22.0.0'

    compile 'org.ligi:AXT:0.33'
    compile 'com.google.guava:guava:18.0'
    compile 'org.ligi:tracedroid:1.4'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'org.ligi:floatlabel:0.2.0'
    compile 'net.sourceforge.jchardet:jchardet:1.0'
    compile 'fr.nicolaspomepuy:discreetapprate:2.0.4@aar'
    compile 'com.android.support:support-annotations:22.0.0'

    def play_services = 'com.google.android.gms:play-services-base:6.5.+'
    withCloudCompile play_services
    withAnalyticsCompile play_services

    withCloudCompile  'com.google.android.gms:play-services-games:6.5.+'
    withCloudCompile  'com.google.android.gms:play-services-plus:6.5.+'
    withCloudCompile  'com.google.android.gms:play-services-appstate:6.5.+'


    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'

    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'
    androidTestCompile 'com.android.support:support-annotations:22.0.0'

    testCompile 'com.squareup.assertj:assertj-android:1.0.0'
    testCompile 'com.android.support:support-annotations:22.0.0'
    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.9'
}
