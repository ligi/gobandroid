buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath('com.stanfy.spoon:spoon-gradle-plugin:1.0.3') {
            exclude module: "guava"
        }
        classpath 'de.mobilej.unmock:UnMockPlugin:0.3.5'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'de.mobilej.unmock'

repositories {
    jcenter()
}

android {
    flavorDimensions "analytics", "cloud"

    productFlavors {
        withAnalytics {
            dimension "analytics"
        }

        noAnalytics {
            dimension "analytics"
        }

        withCloud {
            dimension "cloud"
        }

        noCloud {
            dimension "cloud"
        }

    }

    compileSdkVersion 23
    buildToolsVersion "23"

    defaultConfig {
        versionCode 207
        versionName "2.0.7"
        minSdkVersion 9
        targetSdkVersion 23
        applicationId "org.ligi.gobandroid_hd"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "Gobandroid-$versionName"
    }


    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // important for androidTest
        exclude 'LICENSE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard-project.txt'
        }
    }

} // android

spoon {
    debug = true
}

dependencies {
    compile 'com.android.support:support-v4:23.0.0'
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile 'com.android.support:recyclerview-v7:23.0.0'
    compile 'com.android.support:cardview-v7:23.0.0'
    compile 'com.android.support:design:23.0.0'
    compile 'com.android.support:support-annotations:23.0.0'

    compile 'org.ligi:AXT:0.33'
    compile 'org.ligi:tracedroid:1.4'
    compile 'org.ligi:snackengage:0.4'

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'net.sourceforge.jchardet:jchardet:1.0'

    compile 'com.squareup.okhttp:okhttp:2.4.0'

    withAnalyticsCompile 'com.google.android.gms:play-services-analytics:7.5.0'

    withCloudCompile 'com.google.android.gms:play-services-games:7.5.0'
    withCloudCompile 'com.google.android.gms:play-services-plus:7.5.0'
    withCloudCompile 'com.google.android.gms:play-services-appstate:7.5.0'
    withCloudCompile 'com.google.android.gms:play-services-gcm:7.5.0'
    withCloudCompile 'com.google.android.gms:play-services-plus:7.5.0'

    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'

    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.1'
    androidTestCompile 'com.android.support:support-annotations:23.0.0'

    testCompile 'com.squareup.assertj:assertj-android:1.0.1'
    testCompile 'com.android.support:support-annotations:23.0.0'
    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.10'
}
