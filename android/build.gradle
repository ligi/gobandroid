apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.trevjonez.composer'
composer {
    apkInstallTimeout 600
}

repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
}

android {
    flavorDimensions "analytics", "cloud", "autostart"

    productFlavors {
        withAnalytics {
            dimension "analytics"
        }
        noAnalytics {
            dimension "analytics"
        }
        withCloud {
            dimension "cloud"
        }
        noCloud {
            dimension "cloud"
        }
        withAutostart {
            dimension "autostart"
        }
        noAutostart {
            dimension "autostart"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    android.variantFilter { variant ->
        def analytics = variant.getFlavors().get(0).name
        def cloud = variant.getFlavors().get(1).name

        variant.setIgnore((project.hasProperty("singleFlavor") && (analytics != 'withAnalytics') && (cloud != 'withCloud')) ||
                (analytics == 'withAnalytics' && cloud == 'noCloud') ||
                (analytics == 'noAnalytics' && cloud == 'withCloud'))

    }

    compileSdkVersion 30
    buildToolsVersion '29.0.3'

    defaultConfig {
        versionCode 260
        versionName "2.6.0"
        minSdkVersion 21
        targetSdkVersion 30
        applicationId "org.ligi.gobandroid_hd"
        testInstrumentationRunner "org.ligi.gobandroid_hd.AppReplacingRunner"
        archivesBaseName = "Gobandroid-$versionName"
        vectorDrawables.useSupportLibrary = true
    }


    lintOptions {
        warning 'MissingTranslation'
        warning 'ResourceType'

        // for OKIO
        warning 'InvalidPackage'
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // important for androidTest
        exclude 'LICENSE.txt'

        exclude 'META-INF/lib_release.kotlin_module'
    }

    testOptions {
        animationsDisabled = true
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }

        debug {
            debuggable true
            // to avoid MultiDex http://stackoverflow.com/questions/38329248/dexindexoverflowexception-when-running-androidtests
            minifyEnabled false
        }
    }
} // android

configurations {
    all {
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
    }
}

dependencies {
    implementation "org.permissionsdispatcher:permissionsdispatcher:4.8.0"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:4.8.0"
    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.github.ligi:KAXT:1.0'
    implementation 'com.github.ligi:tracedroid:4.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.github.ligi.snackengage:snackengage-playrate:0.27'
    implementation 'com.github.ligi:ExtraCompats:1.0'
    implementation 'jchardet:jchardet:1.1.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.n3twork.druid:alphanum:1.0.3'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.chibatching.kotpref:kotpref:$kotpref_version"
    implementation "com.chibatching.kotpref:initializer:$kotpref_version"
    withAnalyticsImplementation "com.google.android.gms:play-services-analytics:17.0.0"
    withCloudImplementation "com.google.android.gms:play-services-games:19.0.0"
    withCloudImplementation "com.google.android.gms:play-services-plus:17.0.0"
    withCloudImplementation "com.google.android.gms:play-services-gcm:17.0.0"
    withCloudImplementation('com.google.apis:google-api-services-games:v1-rev98-1.20.0') {
        exclude module: 'jsr305'
    }
    androidTestImplementation 'androidx.test:runner:1.5.1'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'com.github.ligi:trulesk:4.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    testImplementation 'androidx.annotation:annotation:1.1.0'
    testImplementation 'junit:junit:4.13.2'

    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'org.greenrobot:eventbus:3.0.0'

}
